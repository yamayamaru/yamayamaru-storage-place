100 SCREEN 3:COLOR ,,,,2:CLS 3
120 INPUT "KAKUSUU ";N%:INPUT "KAITEN KAKU ";TURN:SIZ = 180
130 PI = 3.14159!: PM2 = PI * 2: PD2 = PI / 2
140 TURN = TURN * PI / 180
150 A2 = PM2 / N%: ANGLE = PI / N%
160 IF TURN >= 0 THEN 190
170     TURN = TURN + PM2
180 GOTO 160
190 T1 = TURN - INT(TURN / A2) * A2 - ANGLE
200 RATIO = COS(ANGLE) / COS(T1)
210 '
220 HEADING = PD2
230 C% = 0
240 IF C% > 1000 THEN 380
250 IF SIZ <=10 THEN 380
260     AG = 0
270     FOR N1% = 0 TO N%
280         A1 = HEADING + AG
290         XPS = COS(A1) * SIZ
300         YPS = SIN(A1) * SIZ
310         GOSUB 400
320         AG = AG + A2
330     NEXT N1%
340     C% = C% + 1
350     HEADING = HEADING + TURN
360     SIZ = SIZ * RATIO
370 GOTO 240
380 END
390 'PLOT
400 IF N1% <> 0 THEN LINE-(XPS+320, -YPS+200),(C% MOD 15)+1 ELSE PSET (XPS+320,-YPS+200),(C% MOD 15)+1
410 RETURN
